prototype(Neos.NeosIo:Content.Column) < prototype(Neos.Neos:ContentComponent) {
    column = Neos.Neos:ContentCollection {
        nodePath = '.'
    }
    className = 'grid-layout--item neos-contentcollection'

    moreLink = ${q(node).property('moreLink')}
    moreLink.@process.convertUris = Neos.Neos:ConvertUris {
        absolute = true
    }
    moreLinkLabel = Neos.Neos:Editable {
        property = 'moreLinkLabel'
    }

    buttonColor = ${q(node).property('style')}
    buttonAlignment = ${q(node).property('alignment')}
    buttonClass = ${'grid-layout--item--more-link' + (this.buttonAlignment ? ' u-align' + this.buttonAlignment : '' )}

    renderer = afx`
        <div class={props.className}>
            {props.column}
            <div class={props.buttonClass} @if.hasLink={props.moreLink}>
                <Neos.NeosIo:Component.Button
                    uri={props.moreLink}
                    color={props.buttonColor}
                    alignment={props.buttonAlignment}
                >
                    {props.moreLinkLabel}
                </Neos.NeosIo:Component.Button>
            </div>
        </div>
    `
}
