prototype(Neos.NeosIo:Content.Column) < prototype(Neos.Neos:ContentComponent) {
    column = Neos.Neos:ContentCollection {
        nodePath = '.'
    }
    className = ${'grid-layout--item neos-contentcollection'}

    moreLink = ${q(node).property('moreLink')}
    moreLink.@process.convertUris = Neos.Neos:ConvertUris {
        absolute = true
    }
    moreLinkLabel = Neos.Neos:Editable {
        property = 'moreLinkLabel'
    }

    renderer = afx`
        <div class={props.className}>
            {props.column}
            <div class="grid-layout--item--more-link" @if.hasLink={props.moreLink}>
                <Neos.NeosIo:Component.Button uri={props.moreLink} color="solidPrimary">
                    {props.moreLinkLabel}
                </Neos.NeosIo:Component.Button>
            </div>
        </div>
    `
}
